apply plugin: 'com.android.application'
apply plugin: 'realm-android'

/**
 * Generate versionCode
 * @return versionCode.
 */
def computeVersionCode() {

    def versions = android.defaultConfig.versionName.split("\\.")
    def versionCode = String.format("%d%02d%02d",
            versions[0].toInteger(),
            versions[1].toInteger(),
            versions[2].toInteger()).toInteger();

    return versionCode
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId "cx.mb.mybarcodereader"
        minSdkVersion 22
        targetSdkVersion 27
        versionName "0.0.1"
        versionCode = computeVersionCode()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

repositories {
    maven {
        url 'https://github.com/WickeDev/stetho-realm/raw/master/maven-repo'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:27.0.2'

    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    compile 'com.google.dagger:dagger:2.13'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.13'

    provided 'org.projectlombok:lombok:1.16.18'
    annotationProcessor 'org.projectlombok:lombok:1.16.18'

    compile 'com.jakewharton.timber:timber:4.6.0'

    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.uphyca:stetho_realm:2.2.2'

    compile 'io.realm:android-adapters:2.1.1'

    compile 'com.google.zxing:core:3.3.1'
    compile 'com.journeyapps:zxing-android-embedded:3.5.0@aar'

    compile 'io.reactivex.rxjava2:rxjava:2.1.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    compile 'de.psdev.licensesdialog:licensesdialog:1.8.3'

    testCompile 'junit:junit:4.12'

    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}
